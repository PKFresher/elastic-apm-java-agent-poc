plugins {
  id 'jacoco'
  id 'com.github.kt3k.coveralls' version '2.6.3'
}

allprojects {

  group = 'com.davidecavestro.elastic.apm'
  version = '0.0.1-SNAPSHOT'

  repositories {
    mavenCentral()
  }

  apply plugin: 'java'
  apply plugin: 'jacoco'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()

    maven { url "http://repo1.maven.org/maven2/" }
  }

  jacocoTestReport {
    reports {
      xml.enabled true
    }
  }
  dependencies {
    testCompile("junit:junit:4.12")
  }
}

def publishedProjects = subprojects.findAll()

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
  description = 'Generates an aggregate report from all subprojects'

  dependsOn(publishedProjects.test)

  additionalSourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  sourceDirectories = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  classDirectories = files(publishedProjects.sourceSets.main.output)
  executionData = files(publishedProjects.jacocoTestReport.executionData)

  doFirst {
    executionData = files(executionData.findAll { it.exists() })
  }

  reports {
    html.enabled = true // human readable
    xml.enabled = true // required by coveralls
  }
}

coveralls {
  sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}
 
tasks.coveralls {
  dependsOn jacocoRootReport
}
